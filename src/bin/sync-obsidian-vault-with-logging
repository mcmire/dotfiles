#!/usr/bin/env zsh

# PATH is empty-ish when this script is running via launchd
source "$HOME/.zsh/_path.zsh"
source "$HOME/.zsh/_homebrew.zsh"

LOG_FILE_PATH=$HOME/Library/Logs/sync-obsidian-vault.log
TEMP_LOG_FILE_PATH="$(mktemp /tmp/$(basename $0).XXXXXX)" || exit 1
CANCELED_EXIT_CODE=100

vault_directory=

parse-args() {
  saw_vault_directory_option=0

  for arg in "$@"; do
    if [[ $saw_vault_directory_option -eq 1 ]]; then
      vault_directory="$arg"
      break
    fi

    if [[ $arg == "--vault-directory" || $arg == "-d" ]]; then
      saw_vault_directory_option=1
    fi
  done

  if [[ -z $vault_directory ]]; then
    echo "USAGE: $0 VAULT_DIRECTORY"
    exit 1
  fi

  if ! [[ -d $vault_directory ]]; then
    echo "ERROR: Could not find vault directory: $vault_directory"
    exit 1
  fi
}

cleanup() {
  rm -f "$TEMP_LOG_FILE_PATH"
}

sync() {
  local command=("$HOME/.bin/sync-obsidian-vault" "$@")
  local vault_directory_name="$(basename "$vault_directory")"
  local output
  local exitcode
  local try=1

  echo "Date: $(date)" | tee -a "$TEMP_LOG_FILE_PATH"
  echo "Current user: $USER" | tee -a "$TEMP_LOG_FILE_PATH"
  echo "Current directory: $PWD" | tee -a "$TEMP_LOG_FILE_PATH"
  echo "Command: ${command[@]}" | tee -a "$TEMP_LOG_FILE_PATH"
  echo | tee -a "$TEMP_LOG_FILE_PATH"

  # Three retries
  while [[ $try -lt 4 ]]; do
    output=$("${command[@]}" 2>&1)
    exitcode=$?

    if [[ "$DEBUG" == "true" ]]; then
      echo "---[ START OF OUTPUT ]-----------"
      echo "$output"
      echo "---[ END OF OUTPUT   ]-----------"
    fi

    if [[ $exitcode -eq 0 || $exitcode -eq $CANCELED_EXIT_CODE ]]; then
      break
    fi

    echo "Got exit code $exitcode, trying again ($try/3 retries)."
    try=$((try+1))
    sleep 5
  done

  echo "$output" >> "$TEMP_LOG_FILE_PATH"
  echo "---------------" >> "$TEMP_LOG_FILE_PATH"
  echo >> "$TEMP_LOG_FILE_PATH"

  cat "$TEMP_LOG_FILE_PATH" >> "$LOG_FILE_PATH"

  if [[ $exitcode -eq 0 ]]; then
    /opt/homebrew/bin/terminal-notifier -title "$vault_directory_name" -message "Synced successfully." || return $?
  elif [[ $exitcode -eq $CANCELED_EXIT_CODE ]]; then
    /opt/homebrew/bin/terminal-notifier -title "$vault_directory_name" -message "Sync was canceled." || return $?
  else
    /opt/homebrew/bin/terminal-notifier -title "$vault_directory_name" -message "Failed to sync. See log file for more: $LOG_FILE_PATH" || return $?
  fi

  return $exitcode
}

main() {
  local exitcode

  parse-args "$@" || return $?

  trap cleanup INT QUIT
  sync "$@"
  exitcode=$?
  cleanup || return $?

  return $exitcode
}

main "$@"
